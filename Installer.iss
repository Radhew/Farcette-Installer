; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Farcette Mod Manager"
#define MyAppVersion "1.0.0"
#define MyAppExeName "FarcetteManager.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{19D3AB36-930B-4213-95C6-56080B00C55F}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
DefaultDirName=C:\Program Files (x86)\Steam\steamapps\common\Arzette The Jewel of Faramore
; "ArchitecturesAllowed=x64compatible" specifies that Setup cannot run
; on anything but x64 and Windows 11 on Arm.
ArchitecturesAllowed=x64compatible
; "ArchitecturesInstallIn64BitMode=x64compatible" requests that the
; install be done in "64-bit mode" on x64 or Windows 11 on Arm,
; meaning it should use the native 64-bit Program Files directory and
; the 64-bit view of the registry.
ArchitecturesInstallIn64BitMode=x64compatible
DefaultGroupName=Farcette
AllowNoIcons=yes
InfoBeforeFile=Farcette_Readme_ManagerVersion.txt
; Remove the following line to run in administrative install mode (install for all users.)
PrivilegesRequired=lowest
OutputDir=Installer
OutputBaseFilename=FarcetteInstaller
SetupIconFile=files\favicon.ico
Compression=lzma
SolidCompression=yes
WizardStyle=modern
DirExistsWarning=no
DisableDirPage=no
DisableProgramGroupPage=no
AppendDefaultDirName=no
; Add 5 GB to the storage requirements, since the installer isn't considering the files we'll need to download later.
ExtraDiskSpaceRequired=3221225472

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
;This is the Farcette data files we'll be downloading from the internet.
Source: "{tmp}\manualfiles.zip"; DestDir: "{app}"; Flags: external deleteafterinstall; Check: IsDownloadNeeded()
Source: "unzip.exe"; DestDir: "{tmp}"
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Messages]
InfoBeforeLabel=This installer will set up DaThings's Farcette: A YouTube Poop Mod for your game installation.
InfoBeforeClickLabel=Please read the following, and click Next to continue.
WizardSelectDir=Select Game Location
SelectDirLabel3=Farcette must be installed directly into the 'Arzette: The Jewel of Faramore' directory.
SelectDirBrowseLabel=If this is your game location, click Next. Otherwise, click Browse.

[CustomMessages]
AdditionalIcons=
CreateDesktopIcon=Place a shortcut to Farcette Mod Manager on the desktop.

[Code]
var
  DownloadPage: TDownloadWizardPage;
  UsagePage: TInputOptionWizardPage;

function OnDownloadProgress(const Url, FileName: String; const Progress, ProgressMax: Int64): Boolean; //The download screen.
begin
  if Progress = ProgressMax then
    Log(Format('Successfully downloaded file to {tmp}: %s', [FileName]));
  Result := True;
end;

function DownloadMode(): Integer; //Are we downloading the Captions, or No Captions version?
begin
  Result := (UsagePage.SelectedValueIndex + 1); //By adding 1, we can say that 0 is not downloading anything
end;

function IsDownloadNeeded(): Boolean; //If the user didn't request any files, we can skip downloading entirely
begin
  Result := (DownloadMode() > 0);
end;

procedure InitializeWizard;
begin
  DownloadPage := CreateDownloadPage(SetupMessage(msgWizardPreparing), SetupMessage(msgPreparingDesc), @OnDownloadProgress);
  UsagePage :=
    CreateInputOptionPage(
      wpInfoBefore, 'Select Mod Version', 'Please choose whether you want your video file to have captions or not. (Due to technical limitations, captions must be baked into the video files.)', 'If you wish to provide the mod files yourself, you can choose neither.', True, False);
  UsagePage.Add('Captions');
  UsagePage.Add('No Captions');
end;

function NextButtonClick(PageId: Integer): Boolean;
begin
  Result := True;
  if (PageId = wpSelectDir) and not FileExists(ExpandConstant('{app}\Arzette.exe')) then begin //Check if we're in the right directory
      MsgBox('No Arzette executable was found in the folder you specified. Please check the location and try again.', mbError, MB_OK);
      Result := False;
      exit;
  end;
  if PageId = wpReady then begin
      DownloadPage.Clear;
      case DownloadMode of //Regardless of which version we bought, it'll be saved as the same file.
        1 : DownloadPage.Add('https://github.com/Radhew/Farcette-Installer/releases/download/v1.0.0/ManualFiles_Captions.zip', 'ManualFiles.zip', '');
        2 : DownloadPage.Add('https://github.com/Radhew/Farcette-Installer/releases/download/v1.0.0/ManualFiles_NoCaptions.zip', 'ManualFiles.zip', '');
      end
      DownloadPage.Show;
      try
        try
          DownloadPage.Download;
          Result := True;
        except
          SuppressibleMsgBox(AddPeriod(GetExceptionMessage), mbCriticalError, MB_OK, IDOK);
          Result := False;
        end;
      finally
        DownloadPage.Hide;
    end;
  end else
    Result := True;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep); //If the mod is disabled when the uninstaller is used, we won't get to take the files with us.
begin
  if CurUninstallStep = usUninstall then
  begin
    if FileExists(ExpandConstant('{app}\captions_original.csv')) then
      if MsgBox('It seems like you still have the mod installed! Please open the Mod Manager and disable it first. If you proceed, the uninstaller will likely fail. Continue anyway?',
        mbConfirmation, MB_YESNO) = IDNO
      then Abort;
  end;
end;

[Run]
; We attempt to unzip the downloaded file here. If we didn't download a file, it'll just silently error out and close.
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent
Filename: "{tmp}\UNZIP.EXE"; Parameters: """{app}\ManualFiles.zip"" -d ""{app}"""; Flags: RunHidden

[UninstallDelete]
; These files come from the zip we download, so the uninstaller won't know it belongs to us otherwise.
Type: files; Name: "{app}\captions_ytp.csv"
Type: files; Name: "{app}\locale_ytp.csv"
Type: files; Name: "{app}\Farcette_Readme.txt"
Type: filesandordirs; Name: "{app}\video_ytp"